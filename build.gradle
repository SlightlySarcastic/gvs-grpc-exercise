plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.8.7'
    id "com.github.ben-manes.versions" version '0.20.0'
}

repositories {
    mavenCentral()
}

def grpcVersion = '1.18.0'
def protobufVersion = '3.6.1'
def protocVersion = '3.6.1'
def undertowVersion = '2.0.17.Final'

dependencies {
    /* ---- Other dependencies */
    implementation 'com.google.guava:guava:27.0.1-jre'
    implementation "io.undertow:undertow-core:${undertowVersion}"
    implementation "io.undertow:undertow-servlet:${undertowVersion}"
    implementation 'org.jboss.resteasy:resteasy-undertow:3.6.2.Final'
    implementation 'com.diffplug.durian:durian:3.4.0'
    implementation 'net.sf.geographiclib:GeographicLib-Java:1.49'
    compileOnly "javax.annotation:javax.annotation-api:1.3.2"

    /* ---- gRPC dependencies */
    implementation "io.grpc:grpc-core:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    /* ---- Test dependencies */
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.4'
}


protobuf {
    generatedFilesBaseDir = "$projectDir/src-gen"

    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'src-gen/main/grpc'
            srcDirs 'src-gen/main/java'
        }
    }
}

application {
    mainClassName = 'de.unia.gvs.grpc.App'
}

// Log test execution to the console for better UX in Gitpod
test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

clean {
    delete protobuf.generatedFilesBaseDir + "/main/grpc/de"
    delete protobuf.generatedFilesBaseDir + "/main/java/de"
}